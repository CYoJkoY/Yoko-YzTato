extends DoubleValueEffect

export(String) var tracking_key = ""
var tracking_key_hash: int = Keys.empty_hash

# =========================== Extension =========================== #
func duplicate(subresources := false) -> Resource:
    var duplication =.duplicate(subresources)
    if tracking_key_hash == Keys.empty_hash and tracking_key != "":
        tracking_key_hash = Keys.generate_hash(tracking_key)

    duplication.tracking_key_hash = tracking_key_hash

    return duplication

static func get_id() -> String:
    return "yztato_stats_chance_on_level_up"

func _generate_hashes() -> void:
    ._generate_hashes()
    tracking_key_hash = Keys.generate_hash(tracking_key)

func apply(player_index: int) -> void:
    if custom_key_hash == Keys.empty_hash: return
    
    var effects: Dictionary = RunData.get_player_effects(player_index)
    effects[custom_key_hash].append([key_hash, value, value2, tracking_key_hash])
    Utils.reset_stat_cache(player_index)

func unapply(player_index: int) -> void:
    if custom_key_hash == Keys.empty_hash: return
    
    var effects: Dictionary = RunData.get_player_effects(player_index)
    effects[custom_key_hash].erase([key_hash, value, value2, tracking_key_hash])
    Utils.reset_stat_cache(player_index)

func get_args(player_index: int) -> Array:
    var tracking_value: float = RunData.ncl_get_effect_tracking_value(tracking_key_hash, player_index)
    var tracking: String = ""
    match tracking_value >= 0:
        true: tracking = Utils.ncl_create_tracking("STATS_GAINED", tracking_value)
        false: tracking = Utils.ncl_create_tracking("STATS_LOST", -tracking_value)

    return [str(value), tr(key.to_upper()), str(value2), tracking]
